fn sec2samp(sec){
    return sec*48000
}
fn diff(input){
    return input - mem(input)
}
fn sigdir(input){
    return diff(input)>0
}
fn countup(active){
    return if (active) (self+1) else 0
}
fn countupn(time,active){
    res = countup(active)
    return if(res<time) res else 0
}
fn hold(time,active){
    return countupn(time,active)>0
}
fn adsr(attack,decay,sustain,release,input){
    atsig = min(1,(self + 1/sec2samp(attack)))
    decsig = max(sustain,(self-1/sec2samp(decay)))
    releasesig =max(0,(self-1/sec2samp(release)))
    at_or_dec = hold(sec2samp(attack),input) 
    at_dec_sus_sig = if (at_or_dec) atsig else decsig
    return if (input>0.5) at_dec_sus_sig else releasesig
}
fn myadsr(input){
    return adsr(0.001,0.5,0.2,0.1,input)
}
PI=3.141595
freq=1900
triggerval = 0
fn setval(val){
    triggerval = val
}
fn trigger(dur)->void{
    setval(1)@now
    setval(0)@(now+dur)
}
fn loopnote()->void{
    freq = (freq*12457)%4809
    println(freq)
    trigger(10000)
    loopnote()@(now+20000)
}
phasor = |f:float|{return (self+f/48000)%1}
saw = |f:float|{return phasor(f)-0.5 }
sq = |f:float|{return (phasor(f)>0.5) - 0.5 }
fn dsp(time){
    return sq(freq) * (triggerval |> myadsr)
}
loopnote()@0
